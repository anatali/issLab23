/* 
 * ================================================================================
 * radargui23.qak		
 * Handles the event sonarrobot : distance( D ) 
 * ================================================================================
 */  
System /* -trace */ radargui23
//mqttBroker "broker.hivemq.com" : 1883  eventTopic "unibo/basicrobot" //"unibo/nat/radar"
mqttBroker "mqtt.eclipseprojects.io" : 1883 eventTopic "unibo/sonar/events"

/*
 *polar:polar(D,A) can be perceived as a dispatch, as a request or as an event
 */
Request  polar        : polar(D,A) 
Reply    fromRadarGui : fromRadarGui(X)     

Dispatch polar        : polar(D,A)   
Event    polar        : polar(D,A) 

Event    sonardata    : distance( V )      
 
Context ctxradargui ip [ host= "localhost"   port= 8038 ]  

/* 
 * -----------------------------------------------------------
 * Embeds the radarpojo and provides message-based interaction
 * (it is the 'exoskeleton - decorator')
 * -----------------------------------------------------------
 */ 
QActor radargui context ctxradargui{ 
[# var DoReply     = false
  var DistanceStr  = "0"
  var Distance     = 0
  var Angle        = "0"
#] 
	State s0 initial{ 
		println("radargui start") 
		run radarPojo.radarSupport.setUpRadarGui()
	} 
	Goto waitForDataToShow
	
	State waitForDataToShow{ 
		println("radargui waiting ... ")
	} 
	Transition t0  
		 whenRequest polar -> showSpotReply
		 whenMsg     polar -> showSpotNoReply
		 whenEvent   polar -> showSpotNoReply
		 whenEvent   sonardata -> showSpot2023
  
	//WARNING: the current message is lost after an empty-move
 	State showSpotNoReply{ 
 		//printCurrentMessage	 
		onMsg( polar : polar(D,A) ) {
			[#
				DistanceStr = payloadArg(0)
			    Distance    = DistanceStr.toInt()
				Angle       = payloadArg(1) 
                DoReply     = false
			#]
		}
 	} 
 	Goto showSpot 
 	
 
	State showSpotReply{
		//printCurrentMessage
   		onMsg( polar : polar(D,A) ) {
			[#
				DistanceStr = payloadArg(0)
			    Distance    = DistanceStr.toInt()
				Angle       = payloadArg(1) 
                DoReply     = true
			#]
 	 	} 
 	 }  
 	 Goto showSpot  
	
	
 	State showSpot{
  		if [# Distance <= 90 #]  {   
			run radarPojo.radarSupport.update(  DistanceStr,  Angle )
			if [# DoReply #] {  replyTo polar with fromRadarGui : fromRadarGui( done($DistanceStr) ) }
 		}   
 	}
	Goto waitForDataToShow


 	State showSpot2023{
 		printCurrentMessage
 		onMsg( sonardata : distance(D) ) {
 			[# DistanceStr = payloadArg(0); Distance = DistanceStr.toInt() #]
	   		if [# Distance <= 90 #]  {   
				run radarPojo.radarSupport.update(  DistanceStr,  "0" )
	 		}   
 		}
 	}
	Transition t0 whenEvent sonardata -> showSpot2023
 
 		
}
